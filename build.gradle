buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.0-beta-2423'
    }
}

apply plugin: 'kotlin'
apply plugin: 'forge'

repositories{
    maven {
        url 'http://jitpack.io'
    }
}

version = "0.0.2"
group= "ru.redenergy.report"
archivesBaseName = "qreport"

minecraft {
    version = "1.7.10-10.13.4.1564-1.7.10"
    runDir = "eclipse"
    srgExtra "PK: kotlin ru/redenergy/report/repack/kotlin"
}


configurations {
    kotlin
    client
    server
    shade

    server.extendsFrom kotlin
    client.extendsFrom kotlin
    shade.extendsFrom(client, server, kotlin)
    compile.extendsFrom shade
}

dependencies {
    kotlin 'org.jetbrains.kotlin:kotlin-stdlib:1.0.0-beta-2423'
    kotlin 'org.jetbrains.kotlin:kotlin-reflect:1.0.0-beta-2423'

    client 'com.github.FRedEnergy:rabbit-gui:-SNAPSHOT'
    
    server 'com.j256.ormlite:ormlite-core:4.48'
    server 'com.j256.ormlite:ormlite-jdbc:4.48'
    server 'org.xerial:sqlite-jdbc:3.7.2'
    server 'mysql:mysql-connector-java:5.1.37'

    testCompile 'junit:junit:4.12'
}

jar{
    configurations.shade.each{ dep ->
        from(project.zipTree(dep)){
            exclude 'META-ING', 'META-INF/**'
        }
    }
    classifier = 'standalone'
}

task serverJar(type: Jar){
    from sourceSets.main.output
    exclude 'ru/redenergy/report/client/**'
    configurations.server.each{ dep ->
        from(project.zipTree(dep)){
            exclude 'META-ING', 'META-INF/**'
        }
    }
    classifier = 'server'
}

task clientJar(type: Jar){
    from sourceSets.main.output
    exclude 'ru/redenergy/report/server/**'
    configurations.client.each{ dep ->
        from(project.zipTree(dep)){
            exclude 'META-ING', 'META-INF/**'
        }
    }
    classifier = 'client'
}

tasks.build.dependsOn('serverJar', 'clientJar')

artifacts{
    jar
    serverJar
    clientJar
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
